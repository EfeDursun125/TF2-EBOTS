float m_checkTimer[TFMaxPlayers];

public void CheckSlowThink(const int client)
{
	if (m_checkTimer[client] < GetGameTime())
	{
		m_isSlowThink[client] = true;

		if (m_isAFK[client])
		{
			PrintToChat(client, "[AFK Bot] You are set AFK.\nType '!afk' in chat to get out of it.");
			PrintCenterText(client, "You are being controlled by a bot, Type !afk to exit.");
		}

		m_class[client] = TF2_GetPlayerClass(client);
		m_team[client] = GetClientTeam(client);
		m_isSniper[client] = false;
		m_isMedic[client] = false;
		m_hasCrossbow[client] = false;
		m_hasWrench[client] = false;
		m_hasSandwich[client] = false;
		m_hasSticky[client] = false;

		int primaryWeapon = GetPlayerWeaponSlot(client, 0);
		if (IsValidEntity(primaryWeapon))
		{
			m_primaryID[client] = TF2_GetWeaponID(primaryWeapon);
			m_primaryClassName[client] = TF2_GetWeaponClassName(primaryWeapon);
			
			if (StrEqual(m_primaryClassName[client], "tf_weapon_sniperrifle"))
				m_isSniper[client] = true;
			else if (StrEqual(m_primaryClassName[client], "tf_weapon_sniperrifle_decap"))
				m_isSniper[client] = true;
			else if (StrEqual(m_primaryClassName[client], "tf_weapon_crossbow"))
				m_hasCrossbow[client] = true;
		}

		int secondaryWeapon = GetPlayerWeaponSlot(client, 1);
		if (IsValidEntity(secondaryWeapon))
		{
			m_secondaryID[client] = TF2_GetWeaponID(secondaryWeapon);
			m_secondaryClassName[client] = TF2_GetWeaponClassName(secondaryWeapon);
			
			if (StrEqual(m_secondaryClassName[client], "tf_weapon_medigun"))
				m_isMedic[client] = true;
			else if (StrEqual(m_secondaryClassName[client], "tf_weapon_lunchbox"))
				m_hasSandwich[client] = true;
			else if (StrEqual(m_secondaryClassName[client], "tf_weapon_pipebomblauncher"))
				m_hasSticky[client] = true;
		}

		int meleeWeapon = GetPlayerWeaponSlot(client, 2);
		if (IsValidEntity(meleeWeapon))
		{
			m_meleeID[client] = TF2_GetWeaponID(meleeWeapon);
			m_meleeClassName[client] = TF2_GetWeaponClassName(meleeWeapon);

			if (StrEqual(m_meleeClassName[client], "tf_weapon_wrench"))
				m_hasWrench[client] = true;
		}

		if (m_meleeID[client] == 656)
		{
			if (m_hasEnemiesNear[client] && TF2_IsPlayerInCondition(client, TFCond_Taunting))
				FakeClientCommandThrottled(client, "taunt");
		}

		if (m_useTeleporter[client] && TF2_IsPlayerInCondition(client, TFCond_TeleportedGlow))
		{
			m_useTeleporter[client] = false;
			m_teleporterEntity[client] = -1;
		}

		if (GameRules_GetProp("m_bPlayingMedieval"))
		{
			if (!IsMoving(client))
			{
				if (m_hasEnemiesNear[client] && m_class[client] != TFClass_Sniper)
					AStarFindPath(-1, FindNearestWaypoint(GetOrigin(m_nearestEnemy[client]), 999999.0, client), client, GetOrigin(m_nearestEnemy[client]));
				else if (m_goalIndex[client] != -1 && !(m_paths[m_goalIndex[client]].activeArea & currentActiveArea))
					SelectObjective(client);
			}
		}

		if (!m_hasWaypoints)
			CheckWalls(client);

		if (m_eBotSenseChance[client] <= 0)
		{
			if (GetConVarInt(EBotSenseMin) < 10)
				SetConVarInt(EBotSenseMin, 10);
			
			if (GetConVarInt(EBotSenseMax) > 90)
				SetConVarInt(EBotSenseMax, 90);
			
			if (GetConVarInt(EBotSenseMin) > GetConVarInt(EBotSenseMax))
				SetConVarInt(EBotSenseMin, GetConVarInt(EBotSenseMax));

			m_eBotSenseChance[client] = crandomint(GetConVarInt(EBotSenseMin), GetConVarInt(EBotSenseMax));
		}

		if (m_difficulty[client] < 0 || m_difficulty[client] > 4)
		{
			if (GetConVarInt(EBotDifficulty) < 0 || GetConVarInt(EBotDifficulty) > 4)
				m_difficulty[client] = crandomint(0, 4);
			else
				m_difficulty[client] = GetConVarInt(EBotDifficulty);
		}

		if (m_eBotDodgeRange[client] <= 0)
		{
			if (GetConVarInt(m_eBotDodgeRangeMin) > GetConVarInt(m_eBotDodgeRangeMax))
				SetConVarInt(m_eBotDodgeRangeMin, GetConVarInt(m_eBotDodgeRangeMax));
			m_eBotDodgeRange[client] = crandomint(GetConVarInt(m_eBotDodgeRangeMin), GetConVarInt(m_eBotDodgeRangeMax));
		}

		if (IsSniper(client) && !m_hasEnemiesNear[client] && !m_hasEntitiesNear[client])
		{
			if (TF2_IsPlayerInCondition(client, TFCond_Zoomed) && IsMoving(client))
				m_buttons[client] |= IN_ATTACK2;
		}
		
		if (m_hasEnemiesNear[client] && m_class[client] == TFClass_Sniper && (IsWeaponSlotActive(client, 1) && m_secondaryID[client] == 751))
			m_buttons[client] |= IN_ATTACK2;
		
		if (m_class[client] == TFClass_Sniper && (IsWeaponSlotActive(client, 0) && m_primaryID[client] == 752))
			m_buttons[client] |= IN_RELOAD;
		
		if (m_hasEnemiesNear[client] && m_class[client] == TFClass_Scout && (IsWeaponSlotActive(client, 2) && m_meleeID[client] == 44 || m_meleeID[client] == 648))
			m_attack2Timer[client] = GetGameTime() + 2.0;
		
		if (m_hasEnemiesNear[client] && m_class[client] == TFClass_Pyro && (IsWeaponSlotActive(client, 0) && m_primaryID[client] == 594))
			m_buttons[client] |= IN_ATTACK2;
		
		if (m_class[client] == TFClass_Scout && (IsWeaponSlotActive(client, 0) && m_primaryID[client] == 448))
			m_buttons[client] |= IN_ATTACK2;
		
		if (!m_hasEnemiesNear[client] && m_class[client] == TFClass_Scout && m_secondaryID[client] == 46 || m_secondaryID[client] == 1145 || m_secondaryID[client] == 163)
		{
			EquipWeaponSlot(client, 1);
			if (IsWeaponSlotActive(client, 1))
				m_buttons[client] |= IN_ATTACK;
		}
		
		if (!m_hasEnemiesNear[client] && m_class[client] == TFClass_Soldier && m_secondaryID[client] == 129 || m_secondaryID[client] == 1001 || m_secondaryID[client] == 226 || m_secondaryID[client] == 354)
		{
			if (100.0 == GetEntPropFloat(client, Prop_Send, "m_flRageMeter"))
			{
				EquipWeaponSlot(client, 1);
				if (IsWeaponSlotActive(client, 1))
					m_buttons[client] |= IN_ATTACK;
			}
		}
		
		if (!m_hasEnemiesNear[client] && m_class[client] == TFClass_Heavy && m_secondaryID[client] == 42 || m_secondaryID[client] == 863 || m_secondaryID[client] == 1002 || m_secondaryID[client] == 1190 || m_secondaryID[client] == 311 || m_secondaryID[client] == 159 || m_secondaryID[client] == 433)
		{
			if (GetClientHealth(client) < (GetMaxHealth(client) / 1.4))
			{
				EquipWeaponSlot(client, 1);
				if (IsWeaponSlotActive(client, 1) && (GetClientHealth(client) < (GetMaxHealth(client) / 1.4)))
					m_buttons[client] |= IN_ATTACK;
			}
		}
		
		if (m_hasEnemiesNear[client] && m_class[client] == TFClass_Engineer && m_meleeID[client] == 589)
		{
			if (GetClientHealth(client) < (GetMaxHealth(client) / 1.3))
			{
				EquipWeaponSlot(client, 2);
				if (IsWeaponSlotActive(client, 2) && (GetClientHealth(client) < (GetMaxHealth(client) / 1.3)))
					FakeClientCommand(client, "eureka_teleport 0");
			}
		}
		
		if (!m_hasEnemiesNear[client] && !m_hasFriendsNear[client] && m_class[client] == TFClass_Medic)
		{
			if (m_meleeID[client] == 304)
			{
				EquipWeaponSlot(client, 2);
			}
			if (m_secondaryID[client] == 35)
			{
				if (GetClientHealth(client) < (GetMaxHealth(client) / 1.4))
				{	
					EquipWeaponSlot(client, 1);
					if (IsWeaponSlotActive(client, 1) && (GetClientHealth(client) < (GetMaxHealth(client) / 1.4)))
						FakeClientCommand(client, "taunt");
				}
			}
		}
		
		if (m_class[client] == TFClass_Medic && m_secondaryID[client] == 998)
		{
			int iProjectile = -1;
			char weaponclass[64];
			GetEntityNetClass(GetPlayerWeaponSlot(client, 1), weaponclass, sizeof(weaponclass));
			if (TF2_IsPlayerInCondition(client, TFCond_OnFire))
				SetEntData(GetPlayerWeaponSlot(client, 1), FindSendPropInfo(weaponclass, "m_nChargeResistType"), 2);
			else if ((iProjectile = FindEntityByClassname(iProjectile, "tf_projectile_*")) != INVALID_ENT_REFERENCE)
			{	
				if (IsValidEntity(iProjectile) && m_team[client] != GetTeamNumber(iProjectile))
				{
					if (GetVectorDistance(GetOrigin(iProjectile), GetOrigin(client), true) < Squaredf(756.0))
						SetEntData(GetPlayerWeaponSlot(client, 1), FindSendPropInfo(weaponclass, "m_nChargeResistType"), 1);
				}
			}
			else
				SetEntData(GetPlayerWeaponSlot(client, 1), FindSendPropInfo(weaponclass, "m_nChargeResistType"), 0);
		}

		if (m_class[client] == TFClass_Pyro && m_primaryID[client] != 594)
		{
			int iProjectile = -1;
			while ((iProjectile = FindEntityByClassname(iProjectile, "tf_projectile_*")) != INVALID_ENT_REFERENCE)
			{
				if (IsValidEntity(iProjectile) && m_team[client] != GetTeamNumber(iProjectile))
				{
					if (GetVectorDistance(GetOrigin(iProjectile), GetOrigin(client), true) <= Squaredf(256.0))
					{
						EquipWeaponSlot(client, 0);
						m_buttons[client] |= IN_ATTACK2;
						m_lookAt[client] = GetOrigin(iProjectile);
						m_pauseTime[client] = GetGameTime() + 2.0;
					}
				}
			}
		}
		
		if (m_class[client] == TFClass_Engineer && m_secondaryID[client] == 528)
		{
			int iProjectile = -1;
			while ((iProjectile = FindEntityByClassname(iProjectile, "tf_projectile_*")) != INVALID_ENT_REFERENCE)
			{
				if (IsValidEntity(iProjectile) && m_team[client] != GetTeamNumber(iProjectile))
				{
					if (GetVectorDistance(GetOrigin(iProjectile), GetOrigin(client), true) <= Squaredf(256.0))
					{
						EquipWeaponSlot(client, 1);
						m_lookAt[client] = GetOrigin(iProjectile);
						if (GetMetal(client) > 65.0)
							m_buttons[client] |= IN_ATTACK2;
						else
							m_buttons[client] |= IN_ATTACK;
					}
				}
			}
		}

		if (m_class[client] == TFClass_DemoMan)
		{
			if (HasSticky(client))
			{	
				m_stickyCount[client] = GetPlayerStickyCount(client);
				
				if (m_stickyCount[client] > 0)
				{
					int search;
					int iSticky = -1;
					while ((iSticky = FindEntityByClassname(iSticky, "tf_projectile_pipe_remote")) != INVALID_ENT_REFERENCE)
					{
						if (!IsValidEntity(iSticky))
							continue;
	
						if (GetEntityThrower(iSticky) != client)
							continue;
	
						for (search = 1; search <= MaxClients; search++)
						{
							if (!IsValidClient(search))
								continue;
	
							if (!m_isAlive[search])
								continue;
	
							if (m_team[client] == m_team[search])
								continue;
	
							if (TF2_IsPlayerInCondition(search, TFCond_Ubercharged))
								continue;
	
							if (GetVectorDistance(GetOrigin(iSticky), GetOrigin(search), true) > Squaredf(125.0))
								continue;
	
							if (!IsVisible(GetCenter(iSticky), GetCenter(search)))
								continue;
							
							m_buttons[client] |= IN_ATTACK2;
						}
					}
				}
			}
			else if (m_hasEnemiesNear[client]) // we have shield
				m_buttons[client] |= IN_ATTACK2;
		}

		if (m_ignoreEnemies[client] < GetGameTime() && ChanceOf(m_eBotSenseChance[client])) // any sus?
		{
			int search;
			for (search = 1; search <= MaxClients; search++)
			{
				if (!IsValidClient(search))
					continue;
				
				if (!m_isAlive[search])
					continue;
				
				if (m_team[client] == m_team[search])
					continue;

				if (m_class[search] != TFClass_Spy)
					continue;

				// only pyro ebots can suspect ebot spies :)))
				if (m_class[client] != TFClass_Pyro && IsEBot(search))
					continue;
				
				if (!IsVisible(GetEyePosition(client), GetEyePosition(search)))
					continue;
				
				if (!IsClientAimingToMe(client, search))
					continue;
				
				if (!ClientViews(client, search, 0.7))
					continue;
				
				m_knownSpy[client] = search;
			}
		}

		if (m_hasEnemiesNear[client] && IsValidClient(m_nearestEnemy[client]) && m_class[m_nearestEnemy[client]] == TFClass_Spy)
			m_knownSpy[client] = m_nearestEnemy[client];
		
		if (m_class[client] == TFClass_Spy)
		{
			if (!TF2_IsPlayerInCondition(client, TFCond_Disguised) && !TF2_IsPlayerInCondition(client, TFCond_Disguising) && !TF2_HasTheFlag(client))
				DisguiseAsEnemy(client);
			
			if (m_hasEntitiesNear[client] && IsValidEntity(m_nearestEntity[client]) && HasEntProp(m_nearestEntity[client], Prop_Send, "m_bHasSapper")) // remember sappable things
				m_knownSentry[client] = m_nearestEntity[client];
		}

		UseVoiceCommand(client);
		
		m_checkTimer[client] = GetGameTime() + crandomfloat(0.95, 1.05);
	}
	else
		m_isSlowThink[client] = false;
}

// automaticly triggers the hiding process
public void CheckHideFromDanger(const int client)
{
	if (!m_isSlowThink[client])
		return;

	if (isOT)
	{
		if (!IsOnDefense(client))
			return;
	}

	if (m_hasEntitiesNear[client])
	{
		SetProcess(client, PRO_HIDE, 60.0, "| process started -> hiding from danger", false, false);
		return;
	}

	if (!m_hasEnemiesNear[client])
		return;

	if (m_enemiesNearPower[client] > m_friendsNearPower[client])
	{
		SetProcess(client, PRO_HIDE, 60.0, "| process started -> hiding from danger", false, false);
		return;
	}

	if (IsOnOffanse(client) && m_friendsNearCount[client] <= m_enemiesNearCount[client])
	{
		SetProcess(client, PRO_HIDE, 60.0, "| process started -> hiding from danger", false, false);
		return;
	}

	bool musthide = false;
	if (!GameRules_GetProp("m_bPlayingMedieval") && m_enemiesNearCount[client] > m_friendsNearCount[client] + 2)
		musthide = true;
	else if (m_class[client] == TFClass_Medic && m_friendsNearCount[client] <= 0)
		musthide = true;
	else if (m_class[client] == TFClass_Spy && m_meleeID[client] == 356)
	{
		if (GetClientHealth(client) < 56.0)
			musthide = true;
	}
	else if (GetClientHealth(client) < (GetMaxHealth(client) / 1.4))
		musthide = true;
	else if (IsValidClient(m_nearestEnemy[client]) && m_class[m_nearestEnemy[client]] == TFClass_Heavy && GetVectorDistance(GetOrigin(client), GetOrigin(m_nearestEnemy[client]), true) < Squaredf(768.0))
		musthide = true;
	else if (m_class[client] == TFClass_Heavy && IsValidClient(m_nearestEnemy[client]) && m_class[m_nearestEnemy[client]] == TFClass_Sniper && GetVectorDistance(GetOrigin(client), GetOrigin(m_nearestEnemy[client]), true) > Squaredf(768.0))
		musthide = true;
	else if (m_hasEnemiesNear[client] && TryToHide(client))
		musthide = true;
	
	if (musthide)
		SetProcess(client, PRO_HIDE, 60.0, "| process started -> hiding from danger", false, false);
}

// automaticly triggers the collect health pack process
public void CheckHealth(const int client)
{
	if (!m_isSlowThink[client])
		return;

	if (isOT)
	{
		if (!IsOnDefense(client))
			return;
	}

	if (m_class[client] == TFClass_Spy && m_meleeID[client] == 356)
	{
		if (GetClientHealth(client) < 55.0)
			m_lowHealth[client] = true;
		else
			m_lowHealth[client] = false;
	}
	else if (GetClientHealth(client) < (GetMaxHealth(client) / 1.4))
		m_lowHealth[client] = true;
	else
		m_lowHealth[client] = false;
	
	if (m_lowHealth[client])
		SetProcess(client, PRO_GETHEALTH, 90.0, "| process started -> need health pack", true, false);
}

// automaticly triggers the collect health pack process
public void CheckAmmo(const int client)
{
	if (!m_isSlowThink[client])
		return;
		
	if (m_class[client] != TFClass_Engineer && m_class[client] != TFClass_Spy && m_class[client] != TFClass_Medic && GetAmmo(client, 0) <= 3)
		m_lowAmmo[client] = true;
	else
		m_lowAmmo[client] = false;
	
	if (m_lowAmmo[client])
		SetProcess(client, PRO_GETAMMO, 90.0, "| process started -> need ammo pack", true, false);
}

public void CheckWalls(const int client)
{
	if (!m_hasEnemiesNear[client] && CheckWall(client))
		m_buttons[client] |= IN_JUMP;
}

stock bool TryToHide(const int client)
{
	if (isOT)
		return false;

	if (m_class[client] == TFClass_Pyro && GetVectorDistance(GetOrigin(client), m_lookAt[client], true) > Squaredf(512.0))
		return true;
	else if (m_class[client] == TFClass_Sniper && GetVectorDistance(GetOrigin(client), m_lookAt[client], true) < Squaredf(512.0))
		return true;

	return false;
}
